load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library")

genrule(
    name = "version_header",
    outs = ["version.h"],
    cmd = "grep '^STABLE_BUILD_GIT_DESCRIBE ' bazel-out/stable-status.txt | sed 's/STABLE_BUILD_GIT_DESCRIBE /#define VERSION \"/g' | sed 's/$$/\"/' > $@",
    cmd_ps = "$$content = Get-Content bazel-out/stable-status.txt | Select-String '^STABLE_BUILD_GIT_DESCRIBE ' | ForEach-Object { $$_.Line -replace '^STABLE_BUILD_GIT_DESCRIBE (.+)', '#define VERSION \"$$1\"' }; $$content | Out-File -Encoding ASCII -NoNewline $@",
    stamp = True,
)

config_setting(
    name = "clang-cl",
    flag_values = {
        "@bazel_tools//tools/cpp:compiler": "clang-cl",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "msvc",
    flag_values = {
        "@bazel_tools//tools/cpp:compiler": "msvc-cl",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "build_shared_libs",
    values = {"define": "build_shared_libs=1"},
)

config_setting(
    name = "enable_pthread",
    values = {"define": "enable_pthread=1"},
)

config_setting(
    name = "enable_arch_native",
    values = {"define": "enable_arch_native=1"},
)

config_setting(
    name = "enable_static_exe",
    values = {"define": "enable_static_exe=1"},
)

C_OPTIONS = select({
    ":msvc": [],
    ":clang-cl": [
        "/W4",
        "-Wconversion",
        "-Wlong-long",
        "-Wmissing-declarations",
        "-Wmissing-prototypes",
        "-Wno-strict-aliasing",
        "-Wshadow",
        "-Wsign-compare",
        "-Wno-sign-conversion",
    ],
    "//conditions:default": [
        "--pedantic-errors",
        "-Wall",
        "-Wconversion",
        "-Wextra",
        "-Wlong-long",
        "-Wmissing-declarations",
        "-Wmissing-prototypes",
        "-Wno-strict-aliasing",
        "-Wshadow",
        "-Wsign-compare",
    ],
})

cc_library(
    name = "bz3",
    srcs = ["src/libbz3.c"],
    hdrs = glob(["include/*.h"]) + [":version_header"],
    copts = C_OPTIONS + select({
        ":enable_arch_native": [
            "-march=native",
            "-mtune=native",
        ],
        "//conditions:default": [],
    }),
    defines = select({
        ":enable_pthread": ["PTHREAD"],
        "//conditions:default": [],
    }),
    includes = ["include"],
    linkstatic = select({
        ":build_shared_libs": False,
        "//conditions:default": True,
    }),
)

cc_binary(
    name = "bzip3",
    srcs = ["src/main.c"],
    copts = C_OPTIONS + select({
        ":enable_arch_native": [
            "-march=native",
            "-mtune=native",
        ],
        "//conditions:default": [],
    }),
    linkstatic = select({
        ":enable_static_exe": True,
        "//conditions:default": False,
    }),
    deps = [":bz3"],
)
